dnl
dnl xfce4-weather-plugin - Show current weather conditions in your panel
dnl
dnl 2003      Benedikt Meurer <benny@xfce.org>
dnl 2006-2007 Nick Schermer <nick@xfce.org>
dnl

dnl ***************************
dnl *** Version information ***
dnl ***************************
m4_define([weather_version_major], [0])
m4_define([weather_version_minor], [10])
m4_define([weather_version_micro], [2])
m4_define([weather_version_build], [@REVISION@])
m4_define([weather_version_tag], [git])
m4_define([weather_version], [weather_version_major().weather_version_minor().weather_version_micro()ifelse(weather_version_tag(), [git], [weather_version_tag()-weather_version_build()], [weather_version_tag()])])

dnl *******************************************
dnl *** Debugging support for git snapshots ***
dnl *******************************************
m4_define([weather_debug_default], [ifelse(weather_version_tag(), [git], [yes], [minimum])])

dnl ***************************
dnl *** Initialize autoconf ***
dnl ***************************
AC_COPYRIGHT([Copyright (c) 2006-2020
        The Xfce development team. All rights reserved.])
AC_INIT([xfce4-weather-plugin], [weather_version], [https://gitlab.xfce.org/panel-plugins/xfce4-weather-plugin], [xfce4-weather-plugin])
AC_PREREQ([2.69])
AC_CONFIG_MACRO_DIRS([m4])

dnl ***************************
dnl *** Initialize automake ***
dnl ***************************
AM_INIT_AUTOMAKE([1.9 dist-bzip2 tar-ustar no-dist-gzip])
AC_CONFIG_HEADERS([config.h])
AM_MAINTAINER_MODE()
m4_ifdef([AM_SILENT_RULES], [AM_SILENT_RULES([yes])])

dnl ********************************
dnl *** Check for basic programs ***
dnl ********************************
AC_PROG_CC()
LT_PATH_LD([])
AC_PROG_INSTALL()
IT_PROG_INTLTOOL([0.35.0])
LT_PREREQ([2.2.6])
LT_INIT([disable-static])
AM_PROG_CC_C_O()

dnl **********************************
dnl *** Check for standard headers ***
dnl **********************************
AC_HEADER_STDC()
AC_CHECK_HEADERS([math.h stdarg.h stddef.h stdlib.h string.h sys/stat.h time.h])
LT_LIB_M
AC_SUBST(LIBM)

dnl ******************************
dnl *** Check for i18n support ***
dnl ******************************
XDT_I18N([@LINGUAS@])

dnl ***********************************
dnl *** Check for required packages ***
dnl ***********************************
XDT_CHECK_PACKAGE([GTK], [gtk+-3.0], [3.22.0])
XDT_CHECK_PACKAGE([GTHREAD], [gthread-2.0], [2.50.0])
XDT_CHECK_PACKAGE([GLIB], [glib-2.0], [2.50.0])
XDT_CHECK_PACKAGE([LIBXFCE4UTIL], [libxfce4util-1.0], [4.12.0])
XDT_CHECK_PACKAGE([LIBXFCE4UI], [libxfce4ui-2], [4.12.0])
XDT_CHECK_PACKAGE([LIBXFCE4PANEL], [libxfce4panel-2.0], [4.12.0])
XDT_CHECK_PACKAGE([LIBXML], [libxml-2.0], [2.4.0])
XDT_CHECK_PACKAGE([SOUP], [libsoup-2.4], [2.42.0])
XDT_CHECK_OPTIONAL_PACKAGE([UPOWER_GLIB], [upower-glib], [0.9.0], [upower],
                           [upower for adapting update interval to power state])


dnl ************************************
dnl *** Special compile time options ***
dnl ************************************
AC_ARG_VAR([GEONAMES_USERNAME], [Username for the GeoNames webservice.
          Please register your own if you're going to distribute the
          package, as requests are limited per hour and day.
          See README for further information.])
AS_IF([test "x$GEONAMES_USERNAME" = x], [GEONAMES_USERNAME="xfce4weatherplugin"])


dnl ***********************************
dnl *** Check for debugging support ***
dnl ***********************************
XDT_FEATURE_DEBUG([weather_debug_default])

AC_CONFIG_FILES([
Makefile
panel-plugin/Makefile
po/Makefile.in
icons/Makefile
icons/liquid/Makefile
icons/liquid/22/Makefile
icons/liquid/48/Makefile
icons/liquid/128/Makefile
icons/liquid-dark/Makefile
icons/liquid-dark/22/Makefile
icons/liquid-dark/48/Makefile
icons/liquid-dark/128/Makefile
icons/simplistic/Makefile
icons/simplistic/22/Makefile
icons/simplistic/48/Makefile
icons/simplistic/128/Makefile
icons/48x48/Makefile
])
AC_OUTPUT

echo
echo "Build Configuration:"
echo
echo "* Debug Support:             $enable_debug"
